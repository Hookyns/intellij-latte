htmlText          <- scriptTagOpen scriptTag htmlText
                   / styleTagOpen styleTag htmlText
                   / htmlTagOpen htmlTag htmlText
                   / HtmlCommentOpen htmlComment htmlText
                   / macro htmlText
                   / Text htmlText
                   / ''
scriptTagOpen     <- HtmlOpenTagOpen
                     &('script' !htmlTagName) htmlTagName
styleTagOpen      <- HtmlOpenTagOpen
                     &('style' !htmlTagName) htmlTagName
htmlTagOpen       <- (HtmlOpenTagOpen / HtmlCloseTagOpen)
                     htmlTagName

scriptTag         <- (HtmlTagClose / HtmlTagCloseEmpty)
                     scriptCdata
                   / macro scriptTag
                   / netteAttr scriptTag
                   / htmlAttr scriptTag
                   / Text scriptTag
                   / ''
scriptCdata       <- HtmlCloseTagOpen
                     &('script' !htmlTagName) htmlTagName htmlTag
                   / macro scriptCdata
                   / Text scriptCdata
                   / ''

styleTag          <- (HtmlTagClose / HtmlTagCloseEmpty)
                     styleCdata
                   / macro styleTag
                   / netteAttr styleTag
                   / htmlAttr styleTag
                   / Text styleTag
                   / ''
styleCdata        <- HtmlCloseTagOpen
                     &('style' !htmlTagName) htmlTagName htmlTag
                   / macro styleCdata
                   / Text styleCdata
                   / ''

htmlTag           <- (HtmlTagClose / HtmlTagCloseEmpty)
                   / macro htmlTag
                   / netteAttr htmlTag
                   / htmlAttr htmlTag
                   / Text htmlTag
                   / ''

htmlTagName       <- HtmlTagNameVoid / HtmlTagName

netteAttr         <- HtmlNAttrName
                     (Ws* HtmlTagEqualSign Ws* netteAttrValue)?
netteAttrValue    <- HtmlAttrSQ (!['] HtmlAttrSQValue)* HtmlAttrSQ
                   / HtmlAttrDQ (!["] HtmlAttrDQValue)* HtmlAttrDQ
                   / HtmlAttrUQValue

htmlAttr          <- HtmlAttrName
                     (Ws* HtmlTagEqualSign Ws* htmlAttrValue)?
htmlAttrValue     <- HtmlAttrSQ htmlAttrValueSQ
                   / HtmlAttrDQ htmlAttrValueDQ
                   / HtmlAttrUQValue
htmlAttrValueSQ   <- HtmlAttrSQ
                   / macro htmlAttrValueSQ
                   / HtmlAttrSQValue htmlAttrValueSQ
                   / ''
htmlAttrValueDQ   <- HtmlAttrDQ
                   / macro htmlAttrValueDQ
                   / HtmlAttrDQValue htmlAttrValueDQ
                   / ''

htmlComment       <- HtmlCommentClose
                   / macro htmlComment
                   / HtmlCommentText htmlComment
                   / ''

macro             <- MacroComment / MacroClassic
macroStart        <- '{' ![ \t\n\r'"{}]
macroEnd          <- '}'
macroComment      <- '*' (!('*' macroEnd) .)* '*'
macroClassic      <- (macroString / '{' macroString* '}')+
macroString       <- "'" ('\\' . / !['\\] .)*  "'"
                   / '"' ('\\' . / !["\\] .)*  '"'
                   / !['"{}] .

# TOKENS
Ws                <- [ \t\n\r]
Text              <- .

HtmlTagName       <- [a-zA-Z0-9:]+
HtmlTagNameVoid   <- ('img' / 'br' / 'input') !HtmlTagName

HtmlTagEqualSign  <- '='
HtmlTagClose      <- '>'
HtmlTagCloseEmpty <- '/>'
HtmlOpenTagOpen   <- '<'
HtmlCloseTagOpen  <- '</'

HtmlAttrName      <- (![ \t\n\r/>={] .)+
HtmlAttrSQ        <- "'"
HtmlAttrSQValue   <- .
HtmlAttrDQ        <- '"'
HtmlAttrDQValue   <- .
HtmlAttrUQValue   <- (![ \t\n\r/>{] .)+

HtmlNAttrName     <- "n:" (![ \t\n\r/>={] .)+

HtmlCommentOpen   <- '<!--' !'>'
HtmlCommentClose  <- '-->'
HtmlCommentText   <- .

MacroComment      <- macroStart macroComment macroEnd
MacroClassic      <- macroStart macroClassic macroEnd


